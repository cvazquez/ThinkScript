# FileName: _DayTradingLabels.tos
# Study Name: _DayTradingLabels
# Date: 20251218
# Author: Carlos Vazquez

# Day Trading Labels
# ------------------
# Detects pre-market and intraday trading times for appropriate ATR values
#
# Displays the following Labels:
#
# *** MISCELLANEOUS ***
# * Closing Price
# * Opening Price
#
# *** SHORT ***
# * ATR Short
# * 1, 2 and 3% over ATR Short
# * DTR Short
# * High of Day
# * 32 Hour High of Day
# * 10, 15 and 20% over CLOSE labels
#
# *** LONG ***
# * ATR Long
# 1, 2 and 3% under ATR Long
# * DTR Long
# * Low of Day
# * 32 Hour Low of Day
# * 10, 15 and 20% under CLOSE labels

declare upper;

input avgATRLength = 14;
input averageType = AverageType.WILDERS;

def indexList = GetSymbol() == "SPY" or
                GetSymbol() == "QQQ" or
                GetSymbol() == "RUT";

def leveragedIndexList = GetSymbol() == "SQQQ" or
                    GetSymbol() == "TQQQ" or
                    GetSymbol() == "SPXL" or
                    GetSymbol() == "SPXS";

def showEquityPercentageLabels = !indexList and !leveragedIndexList;

def isPremarket = GetTime() < RegularTradingStart(GetYYYYMMDD());

def currentPrice = close(priceType = priceType.MARK);
def previousDayClose = close(period = AggregationPeriod.DAY)[1];

def basePeriod = AggregationPeriod.DAY;
def todaysLow = Lowest(low(period =baseperiod)[0], 1);
def todaysHigh = Highest(high(period = baseperiod)[0], 1);

def ATR = if isPremarket then
    MovingAverage(
        averageType,
        TrueRange(
            high(period="DAY")[1],
            close(period="DAY")[1],
            low(period="DAY")[1]
        ),
        avgATRLength
)
else
    WildersAverage(
        TrueRange(
            high(period = AggregationPeriod.DAY),
            close(period = AggregationPeriod.DAY),
            low(period = AggregationPeriod.DAY)
        ),
        avgATRLength
    )
;

def dayStart = GetDay() != GetDay()[1];  # Identifies the start of a new day

# This calculates the number of 1-minute bars in 32 hours
def numBars = 32 * 60;


################# MISCELLANEOUS LABELS ##################

## CLOSING PRICE
    AddLabel(
        yes,
        "C: " + previousDayClose,
        CreateColor(0, 255, 255),
        Location.TOP_LEFT
    );

## OPENING PRICE
    plot openingPrice = open(period = AggregationPeriod.DAY);
    openingPrice.SetDefaultColor(Color.DARK_ORANGE);
    openingPrice.SetLineWeight(2);

    AddLabel(yes, "O: " + openingPrice, Color.DARK_ORANGE, Location.BOTTOM_LEFT);


################# SHORT LABELS ##################

## ATR SHORT
    def prevClosePlusATRShort = previousDayClose + ATR;
    AddLabel (
        yes,
        " ATR+: " + round(prevClosePlusATRShort, 2),
        Color.PINK,
        Location.TOP_LEFT
    );


## Percentages Over ATR
    DefineGlobalColor("overATRBGC", CreateColor(255, 126, 156));

    # 1% over ATR
    def onePercent = 1.00;
    def percent1 = 1 + (onePercent / 100);
    def atrPlus1pct = prevClosePlusATRShort * percent1;

    AddLabel(
        yes,
        "atr+" + onePercent + "% : " + Round(atrPlus1pct, 2),
        GlobalColor("overATRBGC"),
        Location.TOP_LEFT
    );


    # 2% over ATR
    def twoPercent = 2.00;
    def percent2 = 1 + (twoPercent / 100);
    def atrPlus2pct = prevClosePlusATRShort * percent2;

    AddLabel(
        yes,
        "atr+" + twoPercent + "% : " + Round(atrPlus2pct, 2),
        GlobalColor("overATRBGC"),
        Location.TOP_LEFT
    );

    # 3% over ATR
    def threePercent = 3.00;
    def percent3 = 1 + (threePercent / 100);
    def atrPlus3pct = prevClosePlusATRShort * percent3;

    AddLabel(
        yes,
        "atr+" + threePercent + "% : " + Round(atrPlus3pct, 2),
        GlobalColor("overATRBGC"),
        Location.TOP_LEFT
    );


## DTR SHORT
    def hdtr = todaysLow + ATR;

    AddLabel(
        Yes,
        "DTR: " + round(hdtr, 2),
        Color.Yellow,
        Location.TOP_LEFT
    );

## HIGH OF DAY (SHORT)
    DefineGlobalColor("highOfDayBGC", CreateColor(144, 213, 255));

    def dailyHigh = if dayStart then high else Max(high, dailyHigh[1]);  # Tracks the high for the day

    plot HighLine = dailyHigh;  # Plots the daily high line
    HighLine.SetDefaultColor(GlobalColor("highOfDayBGC"));  # You can change the color if desired
    HighLine.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);  # Keep the line horizontal

    AddLabel(
        yes,
        "H: " + dailyHigh,
        GlobalColor("highOfDayBGC"),
        Location.TOP_LEFT
    );


## 32 HOUR LOW OF DAY
    # Get the high over the specified number of bars
    def highOfLast32Hours = Highest(high, numBars);
    def OnePercentOver = round(highOfLast32Hours * 1.01, 2);

    # Plot the 32-hour high line
    AddLabel(
        yes,
        "32H High: " + highOfLast32Hours + " : +1%=" + OnePercentOver,
        GlobalColor("highOfDayBGC"),
        Location.TOP_LEFT
    );


## PERCENTAGES ABOVE CLOSE
    ## 25% above close
        AddLabel(
            showEquityPercentageLabels,
            "Close+25%: " + round(previousDayClose * 1.25, 2),
            Color.MAGENTA,
            Location.TOP_RIGHT
        );

    ## 20% above close
        AddLabel(
            showEquityPercentageLabels,
            "Close+20%: " + round(previousDayClose * 1.2, 2),
            Color.MAGENTA,
            Location.TOP_RIGHT
        );

    ## 15% above close
        AddLabel(
            showEquityPercentageLabels,
            "Close+15%: " + round(previousDayClose * 1.15, 2),
            Color.MAGENTA,
            Location.TOP_RIGHT
        );

    ## 10% above close
        AddLabel(
            showEquityPercentageLabels,
            "Close+10%: " + round(previousDayClose * 1.1, 2),
            Color.MAGENTA,
            Location.TOP_RIGHT
        );


################# LONG LABELS ##################

## ATR LONG
    def prevCloseMinusATRLong = previousDayClose - ATR;
    AddLabel (yes, " ATR-: " + round(prevCloseMinusATRLong, 2) , Color.GRAY, Location.BOTTOM_LEFT);


## Percentages Under ATR
    DefineGlobalColor("underATRBGC", CreateColor(211, 211, 211));

    # 1% under ATR
    def onePercentUnder = 1 - (onePercent / 100);
    def atrMinus1pct = prevCloseMinusATRLong * onePercentUnder;

    AddLabel(
        yes,
        "atr-" + onePercent + "% : " + Round(atrMinus1pct, 2),
        GlobalColor("underATRBGC"),
        Location.BOTTOM_LEFT
    );


    # 2% under ATR
    def twoPercentUnder = 1 - (twoPercent / 100);
    def atrMinus2pct = prevCloseMinusATRLong * twoPercentUnder;

    AddLabel(
        yes,
        "atr-" + twoPercent + "% : " + Round(atrMinus2pct, 2),
        GlobalColor("underATRBGC"),
        Location.BOTTOM_LEFT
    );

    # 3% under ATR
    def threePercentUnder = 1 - (threePercent / 100);
    def atrMinus3pct = prevCloseMinusATRLong * threePercentUnder;

    AddLabel(
        yes,
        "atr-" + threePercent + "% : " + Round(atrMinus3pct, 2),
        GlobalColor("underATRBGC"),
        Location.BOTTOM_LEFT
    );


## DTR LONG
    def ldtr = todaysHigh - ATR;

    AddLabel(
        Yes,
        "DTR: " + round(ldtr, 2),
        Color.RED,
        Location.BOTTOM_LEFT
    );

## LOW OF DAY
    def dailyLow = if dayStart then low else min(low, dailyLow[1]);  # Tracks the low for the day

    plot LowLine = dailyLow;  # Plots the daily low line
    LowLine.SetDefaultColor(Color.WHITE);  # Set the color of the line (you can change the color)
    LowLine.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);  # Keep the line horizontal

    AddLabel(
        yes,
        "L: " + round(dailyLow, 2),
        Color.WHITE,
        Location.BOTTOM_LEFT
    );

## 32 HOUR LOW OF DAY
    # Get the low over the specified number of bars
    def lowOfLast32Hours = if low < dailyLow then low else dailyLow;
    def onePercentUnderLow = round(lowOfLast32Hours * .99, 2);

    AddLabel(
        yes,
        "32H Low: " + lowOfLast32Hours + " : -1%=" + onePercentUnderLow,
        Color.WHITE,
        Location.BOTTOM_LEFT
    );


## PERCENTAGES BELOW CLOSE
    ## 25% below close
        AddLabel(
            showEquityPercentageLabels,
            "Close-25%: " + round(previousDayClose * 0.75, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

    ## 20% below close
        AddLabel(
            showEquityPercentageLabels,
            "Close-20%: " + round(previousDayClose * 0.80, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

    ## 15% below close
        AddLabel(
            showEquityPercentageLabels,
            "Close-15%: " + round(previousDayClose * 0.85, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

    ## 10% below close
        AddLabel(
            showEquityPercentageLabels,
            "Close-10%: " + round(previousDayClose * 0.90, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );


    ## Leveraged Indexes - 3, 6 and 9% below close
        ## 9% below close
        AddLabel(
            leveragedIndexList,
            "Close-9%: " + round(previousDayClose * 0.91, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

        ## 6% below close
        AddLabel(
            leveragedIndexList,
            "Close-6%: " + round(previousDayClose * 0.94, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

        ## 3% below close
        AddLabel(
            leveragedIndexList,
            "Close-3%: " + round(previousDayClose * 0.97, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );


     ## Indexes - 1, 2, 3% below close
        ## 1% below close
        AddLabel(
            indexList,
            "Close-1%: " + round(previousDayClose * 0.99, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

        ## 2% below close
        AddLabel(
            indexList,
            "Close-2%: " + round(previousDayClose * 0.98, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );

        ## 3% below close
        AddLabel(
            indexList,
            "Close-3%: " + round(previousDayClose * 0.97, 2),
            Color.CYAN,
            Location.BOTTOM_RIGHT
        );